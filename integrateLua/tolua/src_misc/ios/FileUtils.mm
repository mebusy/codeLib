//
//  FileUtils.m
//  iosLuaTest
//
//  Created by qibinyi on 2/7/18.
//  Copyright Â© 2018 qibinyi. All rights reserved.
//

#import <Foundation/Foundation.h>

#include "FileUtils.h"

static NSFileManager* s_fileManager = [NSFileManager defaultManager];

std::string FileUtils::getResourcePath() {
    std::string strRet = [[[NSBundle mainBundle] resourcePath] UTF8String];
    strRet.append("/");
    return strRet;
}

std::string FileUtils::getWritablePath()
{
    // save to document folder
    NSArray *paths = NSSearchPathForDirectoriesInDomains(NSDocumentDirectory, NSUserDomainMask, YES);
    NSString *documentsDirectory = [paths objectAtIndex:0];
    std::string strRet = [documentsDirectory UTF8String];
    strRet.append("/");
    return strRet;
}

bool FileUtils::isFileExist(const std::string& strFilePath)
{
    
    
    if (0 == strFilePath.length())
    {
        return false;
    }
    
    bool bRet = false;
    
    if (strFilePath[0] != '/')
    {
        std::string path;
        std::string file;
        size_t pos = strFilePath.find_last_of("/");
        if (pos != std::string::npos)
        {
            file = strFilePath.substr(pos+1);
            path = strFilePath.substr(0, pos+1);
        }
        else
        {
            file = strFilePath;
        }
        
        NSString* fullpath = [[NSBundle mainBundle] pathForResource:[NSString stringWithUTF8String:file.c_str()]
                                                             ofType:nil
                                                        inDirectory:[NSString stringWithUTF8String:path.c_str()]];
        if (fullpath != nil) {
            bRet = true;
        }
    }
    else
    {
        // Search path is an absolute path.
        if ([s_fileManager fileExistsAtPath:[NSString stringWithUTF8String:strFilePath.c_str()]]) {
            bRet = true;
        }
    }
    
    //printf( "ios isFileExist %s : %d \n" , strFilePath.c_str()  ,bRet );
    return bRet;
}
